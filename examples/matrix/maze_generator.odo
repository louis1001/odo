
#{ At 10x10 this some seconds to complete.
 # An example of 10x10
 #
 # ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■
 # • • ■ • • • • • • • ■ • • • ■ • • • • • ■
 # ■ • ■ ■ ■ ■ ■ • ■ • ■ • ■ • ■ ■ ■ ■ ■ • ■
 # ■ • • • • • ■ • ■ • • • ■ • • • • • • • ■
 # ■ ■ ■ ■ ■ • ■ • ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ • ■
 # ■ • • • ■ • • • ■ • • • • • • • • • ■ • ■
 # ■ • ■ • ■ ■ ■ ■ ■ • ■ ■ ■ ■ ■ ■ ■ • ■ • ■
 # ■ • ■ • • • • • • • ■ • • • • • ■ • ■ • ■
 # ■ • ■ ■ ■ • ■ ■ ■ ■ ■ • ■ ■ ■ • ■ ■ ■ • ■
 # ■ • • • ■ • • • • • • • ■ • • • ■ • • • ■
 # ■ ■ ■ • ■ ■ ■ ■ ■ ■ ■ ■ ■ • ■ ■ ■ • ■ ■ ■
 # ■ • ■ • ■ • • • • • • • ■ • ■ • • • ■ • ■
 # ■ • ■ • ■ • ■ ■ ■ • ■ ■ ■ • ■ • ■ ■ ■ • ■
 # ■ • • • ■ • • • ■ • • • ■ • ■ • • • ■ • ■
 # ■ • ■ ■ ■ ■ ■ ■ ■ ■ ■ • ■ • ■ ■ ■ • ■ • ■
 # ■ • ■ • • • • • ■ • • • ■ • • • ■ • • • ■
 # ■ • ■ • ■ ■ ■ • ■ • ■ • ■ ■ ■ • ■ ■ ■ • ■
 # ■ • ■ • ■ • ■ • • • ■ • ■ • ■ • • • • • ■
 # ■ • ■ • ■ • ■ ■ ■ ■ ■ • ■ • ■ ■ ■ ■ ■ ■ ■
 # ■ • • • ■ • • • • • • • • • • • • • • • •
 # ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■ ■
 #
 }#

func to_percent(n: int, m: int): double {
    var cn = n / (m+0.0)
    return cn * 100
}

class Cell {
    var x: int
    var y: int
                       # up     right  down   left
    var walls     =  [ false, false, false, false]
    var neighbors: Neighbor[] =  [ null,  null,  null,  null ]

    var visited = false

    init(x_: int, y_: int) {
        x = x_
        y = y_
    }
}

class Neighbor {
    var cell: Cell
    var dir: int
    init(c_: Cell, dir_: int) { cell = c_; dir = dir_ }
}

class Maze {
    var width = 1
    var height = 1

# I'm not handling this syntax for bi-dimensional lists.
    var cells: Cell[][] = []

    func find_neighbors(x: int, y: int): Neighbor[] {
        var ns: Neighbor[] = [null, null, null, null]

        if y > 0 {
            ns[0] = new Neighbor(cells[y-1][x], 0)
        }
        if x > 0 {
            ns[1] = new Neighbor(cells[y][x-1], 1)
        }

        if y < height-1 {
            ns[2] = new Neighbor(cells[y+1][x], 2)
        }

        if x < width-1 {
            ns[3] = new Neighbor(cells[y][x+1], 3)
        }

        return ns
    }

    init(w: int, h: int) {

        width = w
        height = h

        cells = []#mu::create_matrix(w, h, func(int x, int y) -> new Cell(x, y))

        forange y: h {

            var row: Cell[] = []
            forange x: w {
                push(row, new Cell(x, y))
            }
            push(cells, row)
        }


# Debug this line. It should be working now.
        foreach row : cells {
            foreach c : row {
                c.neighbors = this.find_neighbors(c.x, c.y)
            }
        }
    }

    static func choose(lst: Neighbor[]): Neighbor {
        var rnd_index = randInt(0, length(lst))

        return lst[rnd_index]
    }

    func generate() {
        var initial = cells[0][0];

        var stack = [initial]
        initial.visited = true
        var n_visited = 1
        var max_visit = width * height

        while length(stack) > 0 {
            # Need to fine a way of knowing what type will pop have
            var current: Cell = stack[-1]
            pop(stack)
            write("Generating Maze: ", trunc(to_percent(n_visited, max_visit)), "%\r")

            var possible_neighbors: Neighbor[] = []

            var i = 0
            foreach neigh : current.neighbors {
                if neigh == null { continue }
                var neigh_cell = neigh.cell
                if (!neigh_cell.visited) {
                    push(possible_neighbors, new Neighbor(neigh_cell, i))
                }

                i++
            }

            var neigh_amount = length(possible_neighbors)

            if (neigh_amount > 0) {
                stack += current
                var next = this::choose(possible_neighbors)

                current.walls[next.dir] = true
                next.cell.walls[(next.dir+2)%4] = true
                next.cell.visited = true
                n_visited++

                stack += next.cell
            }
        }
    }

    func print_maze(x: int = 0, y: int = 0) {
        writeln()
        var canvas_height = height*2+1
        var canvas_width = width*2+1

        var path_spaces = " "
        var wall_char = "0"

        var b = [[wall_char]*canvas_width]*canvas_height

        var canvas_max = canvas_width*canvas_height
        var counter = 0

        foreach row : cells {
            foreach c : row {
                var x = c.x*2 + 1
                var y = c.y*2 + 1

                b[y][x] = path_spaces

                var ws = c.walls
                if ws[0] {
                    b[y - 1][x + 0] = path_spaces
                }
                if ws[1] or (c.x == 0 and c.y == 0){
                    b[y + 0][x - 1] = path_spaces
                }
                if ws[2] {
                    b[y + 1][x + 0] = path_spaces
                }
                if ws[3] or (c.x == width-1 and c.y == height-1){
                    b[y + 0][x + 1] = path_spaces
                }
            }
        }

        foreach row : b {
            foreach char : row {
                write(char, " ")
            }
            writeln()
        }
    }
}

var w = 30
var h = 30

var main = new Maze(w, h)
main.generate()
main.print_maze()
