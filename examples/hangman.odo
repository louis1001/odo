
var words = [
    "subsequent",
    "face",
    "cuddly",
    "ignore",
    "request",
    "warlike",
    "brush",
    "hellish",
    "haircut",
    "belong",
    "guard",
    "bolt"
]

var picked = randInt(length(words))
var secret = words[picked]
var w_length = length(secret)
var guessed = "_" * w_length
var letters_tried = [""]
var right_guesses = 0
var errors = 0

var max_errors = 5

func changeSub(str: string, index: int, repl: string): string {
    if (index > length(str)-1){
        writeln("Index out of range!")
        return str
    }
    var result = ""
    forange i : index {
        result += str[i]
    }

    result += repl

    forange (i : index+1, length(str)) {
        result += str[i]
    }

    return result
}

func contains(s: string[], val: string): bool {
    foreach char : s {
        if (char == val) {
            return true
        }
    }

    return false
}

while ((right_guesses < w_length) and (errors < max_errors)) {
    clear()
    writeln("Current guesses: ")
    foreach char : guessed {
        write(char, "")
    }
    writeln()
    writeln("Erros:", errors, "/", max_errors)

    var input = read()
    while length(input) < 1 {
        input = read("Please, input a character\n")
    }
    var guess = input[0]

    if contains(letters_tried, guess) {
        writeln("You used that letter already")
        wait()
        continue
    }

    letters_tried += guess

    var curr_rights = 0
    forange i: w_length {
        if secret[i] == guess {
            curr_rights++
            guessed = changeSub(guessed, i, guess)
            right_guesses++
        }
    }

    if curr_rights == 0 {
        errors++
    }

    writeln()
    clear()
}

if errors == max_errors {
    writeln("I'm sorry!")
} else {
    writeln("Well done!")
}

writeln("The word was: ", secret)
