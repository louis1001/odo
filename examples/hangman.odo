
string words[] = [
    "subsequent",
    "face",
    "cuddly",
    "ignore",
    "request",
    "warlike",
    "brush",
    "hellish",
    "haircut",
    "belong",
    "guard",
    "bolt"
]

int picked = randInt(length(words))
string secret = words[picked]
int w_length = length(secret)
string guessed = "_" * w_length
string letters_tried[] = [""]
int right_guesses = 0
int errors = 0

int max_errors = 5

func changeSub(string str, int index, string repl): string {
    if (index > length(str)-1){
        writeln("Index out of range!")
        return str
    }
    string result = ""
    forange i : index {
        result += str[i]
    }

    result += repl

    forange (i : index+1, length(str)) {
        result += str[i]
    }

    return result
}

func contains(string s[], string val): bool {
    foreach char : s {
        if (char == val) {
            return true
        }
    }

    return false
}

while ((right_guesses < w_length) and (errors < max_errors)) {
    clear()
    writeln("Current guesses: ")
    foreach char : guessed {
        write(char, "")
    }
    writeln()
    writeln("Erros:", errors, "/", max_errors)

    any input = read()
    while length(input) < 1 {
        input = read("Please, input a character\n")
    }
    string guess = input[0]

    if contains(letters_tried, guess) {
        writeln("You used that letter already")
        wait()
        continue
    }

    letters_tried += guess

    int curr_rights = 0
    forange i: w_length {
        if secret[i] == guess {
            curr_rights++
            guessed = changeSub(guessed, i, guess)
            right_guesses++
        }
    }

    if curr_rights == 0 {
        errors++
    }

    writeln()
    clear()
}

if errors == max_errors {
    writeln("I'm sorry!")
} else {
    writeln("Well done!")
}

writeln("The word was: ", secret)
